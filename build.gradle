import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
    id 'io.github.0ffz.github-packages' version '1.0.5'
}

group 'org.cultofclang.minecraft'

if (System.getenv("GITHUB_RUN_NUMBER") != null) plugin_version += ".${System.getenv("GITHUB_RUN_NUMBER")}"

version = plugin_version

java {
    sourceCompatibility JavaVersion.VERSION_16
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://papermc.io/repo/repository/maven-public/'} //Paper
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "io.papermc.paper:paper-api:$serverVersion"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin { kotlinOptions.jvmTarget = "16" }
compileTestKotlin { kotlinOptions.jvmTarget = "16" }

processResources {
    filter ReplaceTokens, tokens: [plugin_version: plugin_version]
}

publishing {
    repositories {
        maven githubPackagePublish.invoke("CultOfClang/BoneHurtingJuice")
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}

//Move into plugins folder
if (project.hasProperty("plugin_path") && plugin_path) {
    task copyJar(type: Copy) {
        from jar
        into plugin_path
        doLast { println("Copied to plugin directory $plugin_path") }
    }
    build.dependsOn copyJar
}
